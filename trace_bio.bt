#!/usr/bin/env bpftrace

#include <linux/blkdev.h>

/*
 * biosnoop.bt   Block I/O tracing tool, showing per I/O latency.
 *               For Linux, uses bpftrace, eBPF.
 *
 * TODO: switch to block tracepoints. Add device, offset, and size columns.
 *
 * This is a bpftrace version of the bcc tool of the same name.
 *
 * 15-Nov-2017	Brendan Gregg	Created this.
 */

BEGIN
{
	printf("%-17s %-16s %-8s %-16s %-8s %-8s ", "TIMESTAMP", "COMM", "PID", "CALLER?", "CALLPID", "DISK");
	printf("%-1s %-12s %-12s %s\n", "T", "SECTOR", "BYTES", "LAT(ns)");
}

kprobe:blk_account_io_start
//kprobe:blk_mq_start_request
{
	@start[arg0] = nsecs;
	@iopid[arg0] = pid;
	@iocomm[arg0] = comm;
	@disk[arg0] = ((struct request *)arg0)->rq_disk->disk_name;
	@rw[arg0] = ((((struct request *)arg0)->cmd_flags) & 255);
	//@rw[arg0] = ((((struct request *)arg0)->cmd_flags) & 255) == 1 ? "W" : "R";
	//@rw[arg0] = (((struct request *)arg0)->cmd_flags & ((1 << REQ_OP_BITS) - 1)) == 1 ? "W" : "R";
	@sector[arg0] = ((struct request *)arg0)->__sector;
	@len[arg0] =  ((struct request *)arg0)->__data_len;
}

kprobe:blk_account_io_done
/@start[arg0] != 0 && @iopid[arg0] != 0 && @iocomm[arg0] != ""/
//@start[arg0] != 0/
{
	$now = nsecs;
	// For tracing, minimize size
	printf("%-17lu %-16s %-8d %-16s %-8d %-8s ",	    
	//printf("%lu %s %d %s %d %s ",	    
	// For pretty printing
	//printf("%-18lu %-16s %-8d %-8s %-1s %-12llu ",	    
		$now, 
		@iocomm[arg0], 
		@iopid[arg0], 
		comm,
		pid,
		@disk[arg0] 
		//@rw[arg0], 
		//@sector[arg0]
		);

	//printf("%d %llu %u %d\n", @rw[arg0], @sector[arg0], @len[arg0], ($now - @start[arg0]));
	printf("%-1d %-12llu %-12u %d\n", @rw[arg0], @sector[arg0], @len[arg0], ($now - @start[arg0]));

	delete(@start[arg0]);
	delete(@iopid[arg0]);
	delete(@iocomm[arg0]);
	delete(@disk[arg0]);
	delete(@rw[arg0]);
	delete(@sector[arg0]);
	delete(@len[arg0]);
}

END
{
	clear(@start);
	clear(@iopid);
	clear(@iocomm);
	clear(@disk);
	clear(@rw);
	clear(@sector);
	clear(@len);
}
