#!/usr/local/bin/bpftrace

#include <linux/fs.h>
#include <linux/mm.h>

// This trace is more general than the others and must print out the command name, else we
// won't be able to know which program performed the action.

BEGIN
{
	printf("%-17s %-8s %-8s %-12s %-12s %s\n", "TIMESTAMP", "PID", "COMM", "OFFSET", "RET (B)", "FILENAME");
}


// ************************************************************
// 	FILE READING
// ************************************************************

// We time the syscall but fetch some extra info from the vfs function

// tracepoint:syscalls:sys_enter_read
//     int __syscall_nr;
//     unsigned int fd;
//     char * buf;
//     size_t count;
tracepoint:syscalls:sys_enter_read,
tracepoint:syscalls:sys_enter_pread64
/ 
comm != "nvsm_core" && comm != "ipmitool" && comm != "bash" && comm != "automount" && comm != "irqbalance" &&
comm != "bpftrace" && comm != "systemd" && comm != "systemd-journal" && comm != "docker" &&
comm != "nvidia-smi" && comm != "networkctl" && comm != "systemd-udevd" && comm != "systemd-resolve" &&
comm != "lspci" && comm != "top" && comm != "smartctl" && comm != "nvipmitool" && comm != "htop"
/
{
	@read_start[tid] = nsecs;
	@read_fd[tid] = args->fd; 
	@requested[tid] = args->count;
}
// ssize_t vfs_read(struct file *file, char __user *buf, size_t count, loff_t *pos)
kprobe:vfs_read
/ @read_start[tid] /
{
	@address[tid] = arg1;
	@offset[tid] = *arg3;
	@filename[tid] = str(((struct file *)arg0)->f_path.dentry->d_name.name);
}

// tracepoint:syscalls:sys_exit_read
//     int __syscall_nr;
//     long ret;
tracepoint:syscalls:sys_exit_read,
tracepoint:syscalls:sys_exit_pread64
/ @read_start[tid] /
{
	$now = nsecs;

	printf("%-17lu %-8d %-8d %s %-12lu %s\n", 
		$now,
		pid,
		comm,
		@offset[tid],
		args->ret,
		@filename[tid]
	);

	delete(@read_start[tid]);
	delete(@read_fd[tid]);
	delete(@address[tid]);
	delete(@offset[tid]);
	delete(@requested[tid]);
	delete(@filename[tid]);
}



// ************************************************************
// 	CLEANUP
// ************************************************************
END 
{
	clear(@read_start);
	clear(@read_fd);
	clear(@address);
	clear(@offset);
	clear(@requested);
	clear(@filename);
}
