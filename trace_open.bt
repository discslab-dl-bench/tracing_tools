#include <linux/fs.h>
#include <linux/path.h>
#include <linux/dcache.h>
#include <linux/types.h>

BEGIN
{
	printf("%-17s %-9s %-8s %-14s %-12s %-6s ", "TIMESTAMP", "TIME", "PID", "COMM", "FUNC", "LAT(ns)");
	printf("%s\n", "FILENAME");
}

/* Only need to trace openat
* From: https://github.com/iovisor/bpftrace/blob/master/docs/tutorial_one_liners.md
* Note: In modern Linux systems (glibc >= 2.26) the open wrapper always calls the openat
*/

tracepoint:syscalls:sys_enter_openat
/ comm == "python" /
{
    @start[tid] = nsecs;
	@func[tid] = "openat";
	@filename[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/@filename[tid]/
{
	printf("%-17lu ", nsecs);

	time("%H:%M:%S  ");

	printf("%-8d %-14s %-12s %-6d %s\n",
		pid,
		comm,
		@func[tid],
		(nsecs - @start[tid]),
		str(@filename[tid])
	);

	delete(@start[tid]);
	delete(@filename[tid]);
}

kprobe:vfs_open
/comm == "python"/
{
	@vstart[tid] = nsecs;
	@vfilename[tid] = ((struct path *)arg0)->dentry->d_name.name;
}

kretprobe:vfs_open
/@vfilename[tid]/
{
	printf("%-17lu ", nsecs);

	time("%H:%M:%S  ");

	printf("%-8d %-14s %-12s %-6d %s\n",
		 pid,
		 comm,
		 "vfs_open",
		 (nsecs - @vstart[tid]),
		 str(@vfilename[tid])
	 );

	 delete(@vstart[tid]);
	 delete(@vfilename[tid]);
}

END 
{
	clear(@start);
	clear(@filename);
	
	clear(@vstart);
	clear(@vfilename);
}
